/*********************************************************************/
/*   PURPOSE: SUBMITS A COMPILE/LINK FOR THE MEMBER BEING EDITED     */
/*            USING THE JCL IN PDS.JCL(COB3XP66) AS MODIFIED BY EDIT */
/*            MACRO COB3EDIT WHICH IS INVOKED BY THIS EDIT MACRO.    */
/*   AUTHOR:  BC0THOR (DICK THORNTON) CREATION DATE: 04/19/2000      */
/*   TYPICAL USER(S): BC0THOR                                        */
/*   PARAMETERS PASSED: NONE                                         */
/*   CDC COMPLIANCE: NO CDC DATE DEPENDENCIES                        */
/*********************************************************************/
/*   MAINTENANCE RECORD: LAST 3 CHANGES                              */
/*********************************************************************/
/*   DATE    LOGONID REASON FOR CHANGE & WHAT WAS DONE               */
/*-------------------------------------------------------------------*/
/*04/19/2000 BC0THOR INITIAL CREATION                                */
/*                                                                   */
/*********************************************************************/
ISREDIT MACRO
CONTROL NOLIST NOSYMLIST NOCONLIST MSG
ISREDIT (COB3MBR) = MEMBER  /* GET NAME OF MEMBER BEING EDITED.      */
IF &COB3MBR = &STR() THEN DO
    SET &ZEDSMSG = &STR(NO MEMBER)
    SET &ZEDLMSG = &STR(MEMBER VARIABLE FROM ISPF IS NULL)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
    END
ISPEXEC VPUT (COB3MBR)     /* WRITE MEMBER NAME TO VARIABLE POOL     */
ISREDIT SAVE               /* SAVE MEMBER BEING EDITED               */
ISPEXEC EDIT DATASET(PDS.JCL(COB3XP66)) MACRO(COB3EDIT) /*EDIT,SUBMIT*/
