/*********************************************************************/
/*   PURPOSE: CONVERTS BRACKETS FOR EDITING C LANGUAGE SOURCE AND    */
/*            PROVIDES A COUNT OF THE LEFT AND RIGHT BRACKETS FOR    */
/*            VALIDATION. FIRST, CONVERTS ALL TRIGRAPH AND TRUE HEX  */
/*            BRACKET VALUES TO THOSE USABLE IN TSO/ISPF EDIT, THEN  */
/*            COUNTS THE LEFT AND RIGHT BRACKETS AND REPORTS ANY     */
/*            INEQUALITY. CIRCUMFLEX ALSO CONVERTED.                 */
/*   AUTHOR:  BC0THOR BASED ON IBM MANUAL. CREATED: 08/14/2000       */
/*   TYPICAL USER(S): BC0THOR                                        */
/*   PARAMETERS PASSED: NONE                                         */
/*   CDC COMPLIANCE: NO DATE DEPENDENCIES                            */
/*********************************************************************/
/*               MAINTENANCE RECORD: LAST 3 CHANGES                  */
/*********************************************************************/
/*   DATE    LOGONID REASON FOR CHANGE & WHAT WAS DONE               */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*********************************************************************/
ISREDIT MACRO
CONTROL NOLIST NOSYMLIST NOCONLIST MSG
IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST
ISREDIT CHANGE ALL C'??(' X'BA'    /*********************************/
ISREDIT CHANGE ALL C'??)' X'BB'    /* CONVERT TRIGRAPHS, X'AD' AND  */
ISREDIT CHANGE ALL X'AD' X'BA'     /* X'BD' TO THE ISPF EDIT SCREEN */
ISREDIT CHANGE ALL X'BD' X'BB'     /* VALUES X'BA' AND X'BB'.       */
                                   /*********************************/
ISREDIT CHANGE ALL X'5F' X'B0'     /* CIRCUMFLEX (HAT)              */

ISREDIT FIND C'[' ALL NX
ISREDIT (N1) = FIND_COUNTS
ISREDIT FIND C']' ALL NX
ISREDIT (N2) = FIND_COUNTS
IF (&N1 ¬= &N2) THEN WRITE UNBALANCED BRACKETS: &N1 LEFT, &N2 RIGHT

ISREDIT FIND C'(' ALL NX
ISREDIT (N1) = FIND_COUNTS
ISREDIT FIND C')' ALL NX
ISREDIT (N2) = FIND_COUNTS
IF (&N1 ¬= &N2) THEN WRITE UNBALANCED PARENTHESES: &N1 LEFT, &N2 RIGHT

ISREDIT FIND C'{'  ALL NX
ISREDIT (N1) = FIND_COUNTS
ISREDIT FIND C'}' ALL NX
ISREDIT (N2) = FIND_COUNTS
IF (&N1 ¬= &N2) THEN WRITE UNBALANCED CURLY BRACES: &N1 LEFT, &N2 RIGHT
END
