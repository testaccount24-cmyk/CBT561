PROC 0
CONTROL MSG
   FREE  ATTRLIST(LSTATTR)
   ATTRIB LSTATTR BLKSIZE(6171) LRECL(121) RECFM(V B) DSORG(PS)
   DELETE '&SYSUID..VLIST' SCRATCH NVSAM
   ALLOC DD(PR2)      DS('&SYSUID..VLIST') MOD SP(1,1) CYL    -
                   UNIT(SYSDA) USING(LSTATTR)   REUSE
STARTIT: +
FREE   DD(PRT,VIN,SYSIN)
ALLOC  DD(PRT) DA(*)  REUSE
/* CLEAR   */
WRITE *******************************************************
WRITE ***            VSAM HELPER                          ***
WRITE ***   THIS UTILITY IS VERY POWERFUL, PLEASE USE     ***
WRITE ***   EXTREME CAUTION, AND BACK UP THE MCF BEFORE   ***
WRITE ***       EXECUTING THE DELETE SYSTEM OPTION.       ***
WRITE ***                                                 ***
WRITE *** (1)  BROWSE VSAM FILE       (2) DELETE SYSTEM   ***
WRITE *** (3)  BROWSE DELETE REPORT   (4) DELETE PACKAGE  ***
WRITE *** (5)  QUIT                                       ***
WRITE ***                                                 ***
WRITE *******************************************************
WRITE ***                                                 ***
WRITENR ** ENTER SELECTION :
READ AA
IF '&AA' = '1' THEN GOTO DUMPIT
IF '&AA' = '2' THEN GOTO DELSYS
IF '&AA' = '3' THEN GOTO DELRPT
IF '&AA' = '4' THEN GOTO DELPKG
IF '&AA' = '5' THEN GOTO ENDIT
GOTO STARTIT
/*******************************************/
/*             DELSYS - DELETE SYSTEM      */
DELSYS: +
WRITE **
WRITENR ** ENTER MCF DATASET :
READ DS
IF '&DS' = ''  THEN GOTO STARTIT
WRITE **
WRITENR ** ENTER SYSTEM TO BE DELETED :
READ SYS
IF '&SYS' = '' THEN GOTO STARTIT
WRITE ** ALL RECORD TYPES CONNECTED WITH THIS SYSTEM WILL BE DELETED **
WRITE ** !!  ELEMENTS, TYPES, SUBSYSTEMS, ETC.. !!!                  **
WRITE ** ALL RECORD TYPES CONNECTED WITH THIS SYSTEM WILL BE DELETED **
WRITE ** !!  ELEMENTS, TYPES, SUBSYSTEMS, ETC.. !!!                  **
WRITENR ** DELETE SYSTEM &SYS ?? Y/N ->
READ AA
IF '&AA'  = '' THEN GOTO STARTIT
IF '&AA' NE 'Y' THEN GOTO STARTIT
DELCONT: +
CONTROL   MSG
ALLOC  DD(VIN) DS('&DS') SHR REUSE
WRITE ** LISTING IS IN DATSET &SYSUID..VLIST   **
DELCALL: +
CALL  'BC0THOR.LOAD.LOAD(BSTVSYS)' 'DELS,&SYS'
WRITENR ***
READ AA
GOTO STARTIT
/*******************************************/
/*             DELPKG - DELETE PACKAGE     */
DELPKG: +
WRITE **
WRITENR ** ENTER PACKAGE DATASET :
READ DS
IF '&DS' = ''  THEN GOTO STARTIT
WRITE **
WRITENR ** ENTER PACKAGE ID TO BE DELETED :
READ SYS
IF '&SYS' = '' THEN GOTO STARTIT
WRITE ** ALL RECORD TYPES CONNECTED WITH THIS PACKAGE WILL BE DELETED**
WRITE ** !!                                     !!!                  **
WRITE ** ALL RECORD TYPES CONNECTED WITH THIS PACKAGE WILL BE DELETED**
WRITE ** !!                                     !!!                  **
WRITENR ** DELETE PACKAGE &SYS ?? Y/N ->
READ AA
IF '&AA'  = '' THEN GOTO STARTIT
IF '&AA' NE 'Y' THEN GOTO STARTIT
DELPCNT: +
CONTROL   MSG
ALLOC  DD(VIN) DS('&DS') SHR REUSE
WRITE ** LISTING IS IN DATSET &SYSUID..VLIST   **
DELPCLL: +
        CALL  'BC0THOR.LOAD.LOAD(BSTVSYS)' 'DELP,&SYS'
WRITENR ***
READ AA
GOTO STARTIT
/*******************************************/
/*             DUMPIT - BROWSE DATASET     */
DUMPIT: +
WRITE **
WRITENR ** ENTER DATASET NAME :
READ DS
IF '&DS' = ''  THEN GOTO STARTIT
FREE  ATTRLIST(DMPATTR)
DELETE '&SYSUID..VDMP' SCRATCH NVSAM
ATTRIB DMPATTR BLKSIZE(5120) LRECL(256) RECFM(F B) DSORG(PS)
ALLOC DD(DMP)      DS('&SYSUID..VDMP') NEW SP(1,1) CYL    -
                   UNIT(SYSDA) USING(DMPATTR)   REUSE
CONTROL   MSG
ALLOC  DD(VIN) DS('&DS') SHR REUSE
CONTROL MSG
CALL  'BC0THOR.LOAD.LOAD(BSTVSYS)' 'DUMP'
ISPEXEC BROWSE DATASET('&SYSUID..VDMP')
DELETE '&SYSUID..VDMP' SCRATCH NVSAM
GOTO STARTIT
DELRPT: +
CONTROL NOMSG
ISPEXEC BROWSE DATASET('&SYSUID..VLIST')
GOTO STARTIT
ENDIT: +
FREE   DD(PRT,VIN,SYSIN,DMP)
