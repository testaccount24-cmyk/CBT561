PROC 0
CONTROL NOLIST NOCONLIST NOSYMLIST ASIS
ISREDIT MACRO
/* ***************************************************************** */
/* * Edit macro to tailor XPEDITER/TSO install JCL                 * */
/* * ------------------------------------------------------------- * */
/* * Customization instructions:                                   * */
/* *                                                               * */
/* * 1.  All lines which should be altered are marked with '<=='.  * */
/* * 2.  Also, lines which are used when installing optional       * */
/* *     product features are marked with 'optional <=='.          * */
/* * 3.  Specific instructions are included below for each         * */
/* *     parameter you may need to alter.  Each block of           * */
/* *     instructions is marked with '>>>' for use with the FIND   * */
/* *     command.                                                  * */
/* * 4.  No changes should be made below the line marked with a    * */
/* *     row of equal signs (=).                                   * */
/* * 5.  This macro will make a copy of the unaltered JCL with a   * */
/* *     member name of memberO (where member is the original      * */
/* *     member name that this macro was executed against EXCEPT   * */
/* *     in the case of member names that are already 8 positions  * */
/* *     long, or where duplicates would occur).  The exceptions   * */
/* *     are: JCLUTILD and JCLUTILI (which become JCLUTIDO and     * */
/* *     JCLUTIIO, respectively).  This copy operation will allow  * */
/* *     you to 'recover' the unaltered JCL in the off chance      * */
/* *     that you may need to rework this macro and re-execute     * */
/* *     it against the JCL.                                       * */
/* * 6.  The members names that this macro will change are:        * */
/* *     IMS005 IMS005V JCLUTIL JCLUTILD JCLUTILI JCL001 JCL002    * */
/* *     JCL003 JCL004 JCL005 JCL006 JCL007 JCL008 JCL009 JCL010   * */
/* *     JCL011 JCL014 JCL015 JCL016 JCL017 JCLSQ1 JCLSQ2 JCLSQ3   * */
/* *     ROS001 ROS002                                             * */
/* *     XTBOOKS XTUNLD JCLRA093 JCLRAUSR JCLLKSVC JCLLKDB2        * */
/* *     XTSVCINS                                                  * */
/* *     JCLDB2SP                                                  * */
/* * ------------------------------------------------------------- * */
/* * Usage instructions:                                           * */
/* *                                                               * */
/* * After you have completed customization of this macro, it can  * */
/* * be used by issuing XTUPDATE on the COMMAND line after         * */
/* * selecting a member.                                           * */
/* *                                                               * */
/* * Important usage notes:                                        * */
/* *                                                               * */
/* * 1.  Once a member has been tailored with XTUPDATE, then       * */
/* *     XTUPDATE cannot be used to tailor that member again.      * */
/* *     (Unless you copy in the memberO backup that this macro    * */
/* *     creates.)                                                 * */
/* * 2.  XTUPDATE is intended for use only with XPEDITER/TSO       * */
/* *     supplied JCL.  Be careful not to use XTUPDATE while       * */
/* *     editting other datasets.                                  * */
/* *                                                               * */
/* ***************************************************************** */

/* ***************************************************************** */
/* * >>> JOB card:                                                 * */
/* *                                                               * */
/* * Sample JOB card information is shown on the lines below.      * */
/* * This JOB card will be inserted into each JCL member.  You     * */
/* * should update these lines to create a valid JOB card.  Note   * */
/* * that the data to be changed for each line of the JOB card is  * */
/* * enclosed by "&STR(" and ")".                                  * */
/* ***************************************************************** */
  SET LINE1 = +
   &STR(//&SYSUID.# JOB (0-0,,TMVS01-UR-XXXX),'DTHORNTON', )  /* <== */
  SET LINE2 = +
   &STR(// CLASS=V,MSGCLASS=Y,NOTIFY=&SYSUID            )     /* <== */
  SET LINE3 = +
   &STR(//JESOUT OUTPUT GROUPID=HDQTRSF2,DEFAULT=YES, )       /* <== */
  SET LINE4 = +
   &STR(//   JESDS=ALL,PAGEDEF=GRYBIG,FORMDEF=GRYBIG    )     /* <== */
  SET LINE5 = +
   &STR(//*                                             )     /* <== */

/* ***************************************************************** */
/* * >>> Dataset names:                                            * */
/* *                                                               * */
/* * Sample dataset names for product libraries are shown below.   * */
/* * You should update the Dataset 'prefix' to your site's         * */
/* * requirements.  The values for XTPREF and CXPREF               * */
/* * should be the same as you used in the XTUNLD job (Step 2 of   * */
/* * the Installation Procedures).                                 * */
/* *                                                               * */
/* * It is suggested that the TSO release be included in the       * */
/* * dataset name for the XPEDITER/TSO load library.  A separate   * */
/* * XPEDITER load library is required for each TSO release.       * */
/* ***************************************************************** */
  SET XTPREF  = SPP.XPEDTSO.V7R0M0                            /* <== */
  SET CXPREF  = SPP.COMPWARE.PROD                             /* <== */

/* ***************************************************************** */
/* * >>> DD parameters for XPEDITER/TSO datasets.                  * */
/* *                                                               * */
/* * DD statements parameters are shown below.  These may need to  * */
/* * be tailored for your site's requirements.  XTVOL and XTUNIT   * */
/* * will be used for XPEDITER datasets.  XTBLKZL will be used for * */
/* * the XPEDITER/TSO load library.                                * */
/* * XTVVOL is the volser for the XOPTIONS VSAM file allocation.   * */
/* * XTTUNIT is the unit type for the install tape, XTTVOL is the  * */
/* * volser of the install tape.  XTBLKZS will be used for the     * */
/* * XPEDITER ISPF datasets.                                       * */
/* ***************************************************************** */
  SET XTVOL   =          /* Specify specific DASD volume serial  <== */
                         /* number after = sign, if required.        */
  SET XTVVOL  =          /* Specify specific volume serial after <== */
                         /* = sign, for VSAM XOPTIONS file.          */
  SET XTTVOL  = XN0701   /* Specify specific volume serial after <== */
                         /* = sign (of the XPEDITER install tape)    */
  SET XTTUNIT = 671      /* Specify the tape unit type after     <== */
                         /* = sign (for the XPEDITER install tape)   */
  SET XTUNIT  = DISK     /* DASD unit type for the XPEDITER libs <== */
                         /* (SYSDA is valid for most sites).     <== */
/*********************************************************************/
/* XTBLKZL is used for XPEDITER LOAD libraries, which are allocted   */
/*   RECFM=U (undefined).                                            */
/*********************************************************************/
  SET XTBLKZL = 27998    /* Change 6144 to desired value.        <== */
                         /*   23476 = 1/2 track on 3380              */
                         /*   27998 = 1/2 track on 3390              */
/*********************************************************************/
/* XTBLKZS is used for XPEDITER ISPF datasets, which are a multiple  */
/*   of 80 (RECFM=FB fixed-blocked).                                 */
/*********************************************************************/
  SET XTBLKZS = 27920    /* Change 6160 to desired value.        <== */
                         /*   23440 = 1/2 track on 3380              */
                         /*   27920 = 1/2 track on 3390              */

/* ***************************************************************** */
/* * >>> Dataset names for IMS libraries.                          * */
/* *                                                               * */
/* * If IMS is used at your site, specify IMSRES as the name of    * */
/* * the IMS load library which contains the module DFSLI000.      * */
/* *                                                               * */
/* * If you will be installing XPEDITER/TSO IMS support,           * */
/* * specify the names for IMS RESLIB and DBDLIB.                  * */
/* *                                                               * */
/* * If you will be installing support for IMS Shared Queues       * */
/* * (Requires IMS 6.1 or higher), specify the name for            * */
/* * IMS EXITLIB (if not, you can ignore the EXITLIB change).      * */
/* *                                                               * */
/* * Replace 'YOUR_IMS_RESLIB' with the name of your IMS RESLIB    * */
/* * Replace 'YOUR_IMS_DBDLIB' with the name of your IMS DBDLIB    * */
/* * Replace 'YOUR_IMS_EXIT_LIBRARY' with the name of your         * */
/* *     IMS EXITLIB                                               * */
/* ***************************************************************** */
  SET IMSRES  = &STR(SYS1.IMSVS.RESLIB)             /* optional  <== */
  SET IMSDBD  = &STR(UCC10.DBDLIB)                  /* optional  <== */
  SET IMSXIT  = &STR(YOUR_IMS_EXIT_LIBRARY)         /* optional  <== */

/* ***************************************************************** */
/* * >>> Dataset name for LE/370 load library.                     * */
/* *                                                               * */
/* * If PL1 or LE/370 is used at your site, specify the name of    * */
/* * the LE/370 linkedit library.  If neither PL1 not LE/370 is    * */
/* * used at your site, leave the 'YOUR.CEE.SCEELKED' default.     * */
/* *                                                               * */
/* * Replace 'YOUR.CEE.SCEELKED' with 'hilvl.cee.sceelked'         * */
/* ***************************************************************** */
  SET CEELKED = &STR(CEE.SCEELKED)                            /* <== */

/* ***************************************************************** */
/* * >>> Dataset names for ROSCOE libraries.                       * */
/* *                                                               * */
/* * If you will be installing XPEDITER/TSO ROSCOE support,        * */
/* * specify the ROSCOE libraries to be used as well as the KEY    * */
/* * to be used.                                                   * */
/* *                                                               * */
/* * Replace 'YOUR.ROSCOE' with the name of the prefix used for    * */
/* * your ROSLIBxx libraries.                                      * */
/* *                                                               * */
/* * Replace 'YOUR.ROSCOE.ROSLIB' with the name of your ROSCOE     * */
/* * load library.                                                 * */
/* *                                                               * */
/* * Replace 'SYS2.XPEDITER.V7R0M0' with the name of the prefix    * */
/* * that will be used for the 'ROSBAT.LOADLIB' which XPEDITER     * */
/* * will allocate which will contain the linkedited XPEDITER      * */
/* * modules to be used by ROSCOE.                                 * */
/* *                                                               * */
/* * Replace 'XPEDITER.BATCON' with the name of the ROSCOE library * */
/* * KEY that will be used for XPEDITER.                           * */
/* ***************************************************************** */
  SET ROSPREF = &STR(YOUR.ROSCOE)                   /* optional  <== */
  SET ROSLOAD = &STR(YOUR.ROSCOE.ROSLIB)            /* optional  <== */
  SET ROSXPD  = &STR(XPEDITER.BATCON)               /* optional  <== */
  SET ROSBAT  = &STR(SYS2.XPEDITER.V7R0M0)          /* optional  <== */

/* >>> ============================================================= */
/* = S T O P === S T O P === S T O P === S T O P === S T O P ======= */
/* == S T O P === S T O P === S T O P === S T O P === S T O P ====== */
/* === S T O P === S T O P === S T O P === S T O P === S T O P ===== */
/* ==== S T O P === S T O P === S T O P === S T O P === S T O P ==== */
/* >>> = S T O P - DO NOT MODIFY ANY STATEMENTS BELOW THIS LINE!! << */
/* ====== S T O P === S T O P === S T O P === S T O P === S T O P == */
/* ======= S T O P === S T O P === S T O P === S T O P === S T O P = */
/* ======== S T O P === S T O P === S T O P === S T O P === S T O P  */
/* ========= S T O P === S T O P === S T O P === S T O P === S T O P */
/* ================================================================= */
  SET BKPREF  =       /* softcopy */
  SET XTSFTVB = 27998 /* softcopy */
  SET XTSFTFB = 24576 /* softcopy */

  SET SRCHLVL = V7R0M0
  ISREDIT EXCLUDE ALL
  ISREDIT FIND ALL 'tailored with '                /* test recursion */
  IF &LASTCC = 0 THEN GOTO NOREDO
  ISREDIT RESET
  ISREDIT (XMEMBER) = MEMBER           /* get name of current member */

  ISREDIT FIND ALL 'XPEDITER/TSO'               /* is it our member? */
  IF &LASTCC = 0 THEN GOTO ITSOURS
  ISREDIT FIND ALL 'XPEDITER.TSO'               /* is it our member? */
  IF &LASTCC = 0 THEN GOTO ITSOURS
  ISREDIT FIND ALL '.XPEDITER.'                 /* is it our member? */
  IF &LASTCC = 0 THEN GOTO ITSOURS
  ISREDIT FIND ALL 'XPEDITER/IMS'               /* is it our member? */
  IF &LASTCC = 0 THEN GOTO ITSOURS
  ISREDIT FIND ALL 'COMPWARE.CX'                /* is it our member? */
  IF &LASTCC = 0 THEN GOTO ITSOURS
  GOTO NOTINST

ITSOURS: &NULL
  IF &XMEMBER = IMS005   OR &XMEMBER = IMS005V  OR +
     &XMEMBER = JCLUTIL  OR &XMEMBER = JCLUTILD OR +
     &XMEMBER = JCLUTILI OR &XMEMBER = JCL001   OR +
     &XMEMBER = JCL002   OR &XMEMBER = JCL003   OR +
     &XMEMBER = JCL004   OR &XMEMBER = JCL005   OR +
     &XMEMBER = JCL006   OR &XMEMBER = JCL007   OR +
     &XMEMBER = JCL008   OR &XMEMBER = JCL009   OR +
     &XMEMBER = JCL010   OR &XMEMBER = JCL011   OR +
     &XMEMBER = JCL014   OR &XMEMBER = JCL015   OR +
     &XMEMBER = JCL016   OR &XMEMBER = JCL017   OR +
     &XMEMBER = JCLSQ1   OR &XMEMBER = JCLSQ2   OR +
     &XMEMBER = JCLSQ3   OR &XMEMBER = JCLLKDB2 OR +
     &XMEMBER = JCLRAUSR OR &XMEMBER = JCLRA093 OR +
     &XMEMBER = JCLLKSVC OR &XMEMBER = XTSVCINS OR +
     &XMEMBER = ROS001   OR &XMEMBER = ROS002   OR +
     &XMEMBER = XTBOOKS  OR &XMEMBER = XTUNLD   OR +
     &XMEMBER = JCLDB2SP                           +
     THEN GOTO PROCESS
  GOTO NOTINST
PROCESS: &NULL
  SET NEWMEMB=
  IF &XMEMBER = JCLUTILD THEN +
     SET NEWMEMB = JCLUTIDO
  IF &XMEMBER = JCLUTILI THEN +
     SET NEWMEMB = JCLUTIIO
  IF &NEWMEMB¬= THEN GOTO HAVESET

  IF &LENGTH(&XMEMBER) > 7 THEN +
     SET NEWMEMB = &STR(&SUBSTR(1:7,&XMEMBER)O)
  ELSE +
     SET NEWMEMB = &XMEMBER.O

  IF &NEWMEMB= THEN GOTO NOTINST

HAVESET: &null
  ISREDIT CREATE &NEWMEMB .ZFIRST .ZLAST  /* create backup memberO */
  IF &XMEMBER = JCL014 THEN GOTO XMSGOPT

  ISREDIT EXCLUDE ALL '//*' 1     /* Do not modify JCL comment lines */

  ISREDIT FIND ALL NX '//' 1                          /* is it JCL?  */
  IF &LASTCC ¬= 0 THEN GOTO NOTINST             /* no, don't install */
  ISREDIT FIND ALL NX ' DD '
  IF &LASTCC ¬= 0 THEN GOTO NOTINST

ISAJOB: &null
  SET INSTNO=
  IF &SUBSTR(1:3,&XMEMBER) EQ JCL THEN +
     SET INSTNO = &SUBSTR(4:6,&XMEMBER)
  IF &XMEMBER = XTSVCINS +
     THEN GOTO CHG1
  IF &XMEMBER ¬= IMS005V +
     AND &XMEMBER ¬= IMS005 +
     AND &XMEMBER ¬= XTUNLD +
     AND &XMEMBER ¬= XTBOOKS +
     AND &XMEMBER ¬= ROS001 +
     AND &XMEMBER ¬= ROS002 +
     AND &INSTNO= THEN GOTO NOTINST
  ISREDIT LINE_BEFORE 1 = DATALINE &STR("&LINE1")
  ISREDIT LINE_AFTER  1 = DATALINE &STR("&LINE2")
  ISREDIT LINE_AFTER  2 = DATALINE &STR("&LINE3")
  ISREDIT LINE_AFTER  3 = DATALINE &STR("&LINE4")
  ISREDIT LINE_AFTER  4 = DATALINE &STR("&LINE5")
  SET LINEX = +
     &STR(//* &ZDATE &ZTIME - &SYSUID tailored with &SYSICMD..)
  ISREDIT LINE_AFTER  5 = DATALINE &STR("&LINEX")
  IF &XMEMBER = XTUNLD THEN GOTO UNLDSTEP
  ISREDIT CHANGE ALL NX '//YOUR JOB CARD HERE'   '//******************'

  IF &XMEMBER = ROS001 OR &XMEMBER = ROS002 +
     THEN GOTO ROSCOE

CHG1: &null
  ISREDIT CHANGE ALL NX 'DSN=SYS2.XPEDITER.&SRCHLVL' +
                        'DSN=&XTPREF.'
  ISREDIT CHANGE ALL NX 'COMPWARE.CX'     &CXPREF

  IF &INSTNO = 001 OR &INSTNO = 002 OR &INSTNO = 005 OR +
     &INSTNO = 006 OR &INSTNO = 007 OR &INSTNO = 009 OR +
     &INSTNO = LKS OR +
     &INSTNO = 017 THEN GOTO SIMPLE1
  IF &XMEMBER = XTSVCINS +
     THEN GOTO SIMPLE1
  IF &INSTNO = 003 THEN GOTO LE370LNK
  IF &INSTNO = 004 THEN GOTO IMSSTEP
  IF &INSTNO = 011 THEN GOTO LIBSPROC
  IF &INSTNO = 014 THEN GOTO XMSGOPT
  IF &INSTNO = 015 OR &XMEMBER = IMS005V OR +
     &XMEMBER = IMS005 THEN GOTO VSAM
  IF &INSTNO = 008 OR &INSTNO = 010 OR &INSTNO = 016 OR +
     &XMEMBER = JCLUTILI OR &XMEMBER = JCLUTILD OR +
     &XMEMBER = JCLUTIL +
     THEN GOTO NOTCX
  IF &INSTNO = SQ1 OR &INSTNO = SQ2 OR &INSTNO = SQ3 +
     THEN GOTO IMSSTEP
  IF &XMEMBER = XTBOOKS THEN GOTO UNLDBOOK
  GOTO NOTINST

LE370LNK: &null
  IF &CEELKED = YOUR.CEE.SCEELKED THEN +
    DO
      ISREDIT CHANGE ALL 'DSN=YOUR.CEE.SCEELKED' 'DSN=NULLFILE'
      ISREDIT FIND ALL 1 '//LKED4 '
      ISREDIT (ROW,COL) = CURSOR
      SET L1 = +
        &STR("//*        PARM='RENT,REUS,REFR,LIST,SIZE=1024K'")
      SET L2 = +
        &STR("//         PARM='RENT,REUS,REFR,NCAL,LIST,SIZE=1024K'")
      ISREDIT LINE &EVAL(&ROW+1) = &L1
      ISREDIT LINE &EVAL(&ROW+2) = &L2
    END
  ELSE +
    DO
      ISREDIT CHANGE ALL NX "YOUR.CEE.SCEELKED"      "&CEELKED."
      ISREDIT FIND ALL 1 '//LKED4 '
      ISREDIT (ROW,COL) = CURSOR
      SET L1 = +
        &STR("//         PARM='RENT,REUS,REFR,LIST,SIZE=1024K'")
      SET L2 = +
        &STR("//*        PARM='RENT,REUS,REFR,NCAL,LIST,SIZE=1024K'")
      ISREDIT LINE &EVAL(&ROW+1) = &L1
      ISREDIT LINE &EVAL(&ROW+2) = &L2
    END
  GOTO NOTCX

UNLDBOOK: &null
  ISREDIT CHANGE ALL NX 'DSN=CWBOOKS' 'DSN=&BKPREF.'
  ISREDIT CHANGE ALL NX ' VOL=SER=DDDDDD, '      ' VOL=SER=&XTVOL., '
  ISREDIT CHANGE ALL NX ' UNIT=NNNNNN, '         ' UNIT=&XTUNIT., '
  ISREDIT CHANGE ALL NX '//* YOUR JOB CARD HERE'   '//****************'
  ISREDIT CHANGE ALL NX 'DCB=(RECFM=FB,BLKSIZE=0,' +
                        'DCB=(RECFM=FB,BLKSIZE=&XTSFTFB.,'
  ISREDIT CHANGE ALL NX 'DCB=(RECFM=VB,BLKSIZE=8000,' +
                        'DCB=(RECFM=VB,BLKSIZE=&XTSFTVB.,'
  GOTO NOTCX

UNLDSTEP: &null
  ISREDIT CHANGE ALL NX '<==== CHECK ' '   <=CHK '
  SET L1 = &STR("XTSOR='SYS2.XPEDITER.&SRCHLVL")
  SET L2 = &STR("XTSOR='&XTPREF")
  ISREDIT CHANGE ALL NX &L1 &L2
  SET L1 = &STR("XTOBJ='SYS2.XPEDITER.&SRCHLVL")
  SET L2 = &STR("XTOBJ='&XTPREF")
  ISREDIT CHANGE ALL NX &L1 &L2
  SET L1 = &STR("CXSOR='COMPWARE.CX")
  SET L2 = &STR("CXSOR='&CXPREF")
  ISREDIT CHANGE ALL NX &L1 &L2
  SET L1 = &STR("CXOBJ='COMPWARE.CX")
  SET L2 = &STR("CXOBJ='&CXPREF")
  ISREDIT CHANGE ALL NX &L1 &L2
  ISREDIT CHANGE ALL NX "DUNIT='SYSDA'" "DUNIT='&XTUNIT'"
  ISREDIT CHANGE ALL NX "TUNIT='TAPE'" "TUNIT='&XTTUNIT'"
  ISREDIT CHANGE ALL NX ' VOL=SER=DDDDDD, '      ' VOL=SER=&XTVOL., '
  ISREDIT CHANGE ALL NX ',SER=TTTTTT)'           ',SER=&XTTVOL.)'
  ISREDIT CHANGE ALL NX '//YOUR TAPE JOB CARD HERE'   '//*************'
  GOTO NOTCX

LIBSPROC: &null
  ISREDIT CHANGE ALL NX 'LIBS=SYS2.XPEDITER.&SRCHLVL' +
                        'LIBS=&XTPREF.'
  GOTO NOTCX

IMSSTEP: &null
  ISREDIT CHANGE ALL NX 'DSN=YOUR_IMS_RESLIB' +
                        'DSN=&IMSRES.'
  ISREDIT CHANGE ALL NX 'DSN=YOUR_IMS_EXIT_LIBRARY' +
                        'DSN=&IMSXIT.'
  GOTO NOTCX

SIMPLE1: &null
  ISREDIT CHANGE ALL NX ' VOL=SER=DDDDDD, '      ' VOL=SER=&XTVOL., '
  ISREDIT CHANGE ALL NX ' UNIT=SYSDA, '          ' UNIT=&XTUNIT., '
  IF &INSTNO = 002 THEN GOTO LIBBLKZ
  IF &INSTNO = 007 OR &INSTNO = 009 THEN GOTO ISPFBLKZ
  GOTO NOTCX

ISPFBLKZ: &null
  ISREDIT CHANGE ALL NX ',BLKSIZE=6160,'        ',BLKSIZE=&XTBLKZS.,'
  GOTO NOTCX

LIBBLKZ: &null
  ISREDIT CHANGE ALL NX ',BLKSIZE=23476)'        ',BLKSIZE=&XTBLKZL.)'
  GOTO NOTCX

VSAM: &null
  IF &XTVVOL= THEN +
    DO
      SET L1 = &STR(/*VOLUME(SMS)*/)
      ISREDIT CHANGE ALL NX '  VOLUME(VVVVVV)  ' '&L1 '
    END
  ELSE
    ISREDIT CHANGE ALL NX 'VOLUME(VVVVVV)'  'VOLUME(&XTVVOL.)'

  ISREDIT CHANGE ALL NX ' DELETE SYS2.XPEDITER.&SRCHLVL' +
                        ' DELETE &XTPREF'
  ISREDIT CHANGE ALL NX ' NAME(SYS2.XPEDITER.&SRCHLVL' +
                        ' NAME(&XTPREF'
  IF &INSTNO = 015 THEN GOTO NOTCX

  ISREDIT CHANGE ALL NX 'NAME(SYS2.XPEDITER.&SRCHLVL' +
                        'NAME(&XTPREF'
  ISREDIT CHANGE ALL NX ' NAME (SYS2.XPEDITER.&SRCHLVL' +
                        ' NAME (&XTPREF'
  ISREDIT CHANGE ALL NX 'DSN=YOUR.IMS.RESLIB' +
                        'DSN=&IMSRES.'
  ISREDIT CHANGE ALL NX 'DSN=YOUR.IMS.DBDLIB' +
                        'DSN=&IMSDBD.'
  IF &XTVVOL= THEN +
    DO
      SET L1 = &STR(/*VOLUME(SMS)*/)
      ISREDIT CHANGE ALL NX ' VOLUMES(VVVVVV) ' '&L1 '
    END
  ELSE
    ISREDIT CHANGE ALL NX 'VOLUMES(VVVVVV)'  'VOLUME(&XTVVOL.)'
  GOTO NOTCX

ROSCOE: &null
  ISREDIT CHANGE ALL NX 'COMPWARE.CX'     &CXPREF
  ISREDIT CHANGE ALL NX ' VOL=SER=DDDDDD, '      ' VOL=SER=&XTVOL., '
  ISREDIT CHANGE ALL NX ' UNIT=SYSDA, '          ' UNIT=&XTUNIT., '
  ISREDIT CHANGE ALL NX 'DSN=YOUR.ROSCOE.ROSLIB,' +
                        'DSN=&ROSLOAD.,'
  ISREDIT CHANGE ALL NX 'DSN=YOUR.ROSCOE'       'DSN=&ROSPREF.'
  ISREDIT CHANGE ALL NX 'OUTKEY=XPEDITER.BATCON' +
                        'OUTKEY=&ROSXPD.'
  ISREDIT CHANGE ALL NX 'DSN=SYS2.XPEDITER.V7R0M0.ROSBAT.LOADLIB' +
                        'DSN=&ROSBAT..ROSBAT.LOADLIB'
  ISREDIT CHANGE ALL NX 'DSN=SYS2.XPEDITER.&SRCHLVL' +
                        'DSN=&XTPREF.'
  ISREDIT CHANGE ALL NX '<==== YOUR ROSCOE LOAD' ' '
  GOTO NOTCX

NOTCX: &null
  ISREDIT FIND # FIRST
EXIT CODE(0)

NOREDO: &null
  ISREDIT (XLINE,XCOL) = CURSOR
  ISREDIT CURSOR = &XLINE 0
      SET &ZEDSMSG = &STR(Already tailored)
      SET &ZEDLMSG = +
        &STR(NO CHANGES MADE.  Member already tailored with &SYSICMD..)
      ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(4)

XMSGOPT: &null
  ISREDIT (XLINE,XCOL) = CURSOR
  ISREDIT CURSOR = &XLINE 0
    SET &ZEDSMSG = &STR(XMESSAGE not required)
    SET &ZEDLMSG = +
      &STR(NO CHANGES MADE.  The XMESSAGE dataset is no longer required-
.)
    ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(4)

NOTINST: &null
  ISREDIT (XLINE,XCOL) = CURSOR
  ISREDIT CURSOR = &XLINE 0
    SET &ZEDSMSG = &STR(Not XPED/TSO install)
    SET &ZEDLMSG = +
      &STR(NO CHANGES MADE.  This is not an XPEDITER/TSO install member-
.)
    ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(4)

